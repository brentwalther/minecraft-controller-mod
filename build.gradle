buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.5'
    }
}
apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

allprojects {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        compile 'com.github.strikerx3:jxinput:ca55984'
    }
}

version = "1.0"
group = "net.brentwalther.controllermod"
archivesBaseName = "controllermod"

minecraft {
    version = "1.12.2-14.23.2.2634"
    runDir = "run"
    mappings = "snapshot_20171003"
}

dependencies {
    compile group: 'com.google.guava', name: 'guava', version: '23.5-jre'
    compile 'com.google.protobuf:protobuf-java:3.0.0'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }

    // Output the files in our regular source tree
    generatedFilesBaseDir = "$projectDir/src/"
}
